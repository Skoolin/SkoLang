grammar Demo;

program: programPart+ ;

programPart: statement			#MainStatement
           | functionDefinition	#ProgPartFunctionDefinition
           ;

statement: print ';'
		 | println ';'
		 | systemCall ';'
		 | functionCall ';'
         | varDeclaration ';'
         | assignment ';'
         | typeDeclaration
         | branch
         | loop
         ;

branch: 'if' '(' condition=expression ')' onTrue=block 'else' onFalse=block
	  ;

loop: 'while' '(' condition=expression ')' body=block
	;

block: '{' statement* '}' ;

expression: left=expression '/' right=expression #Div
          | left=expression '*' right=expression #Mult
          | left=expression '-' right=expression #Minus
          | left=expression '+' right=expression #Plus
          | left=expression operator=('<' | '<=' | '>' | '>='| '==' | '<>') right=expression #Relational
          | left=expression '&&' right=expression #And
          | left=expression '||' right=expression #Or
          | typeName=IDENTIFIER '.' param=IDENTIFIER  #typeReference
          | varName=IDENTIFIER '[' index=expression ']'	#ArrayElement
          | number=NUMBER 						 #Number
          | txt=STRING 							 #String
          | varName=IDENTIFIER 					 #Variable
          | constructorCall						 #Constructor
          | systemCall							 #SystemFunctions
          | functionCall 						 #funcCallExpression
          ;

varDeclaration: type=object varName=IDENTIFIER
			  | type=object varName=IDENTIFIER '=' expr=expression
			  ;

typeDeclaration: 'type' typeName=IDENTIFIER '{' params=parameterDeclaration '}' ;

assignment: varName=IDENTIFIER '[' index=expression ']' '=' expr=expression
		  | varName=IDENTIFIER '=' expr=expression
		  ;

println: 'print(' argument=expression ')' ;

print: 'out(' argument=expression ')' ;

functionDefinition: type=object funcName=IDENTIFIER '(' params=parameterDeclaration ')' '{' statements=statementList 'return' returnValue=expression ';' '}' ;

parameterDeclaration: declarations+=varDeclaration (',' declarations+=varDeclaration)*
                    |
                    ;

statementList: statement* ;

systemCall: funcName=SYSTEMFUNC '(' arguments=expressionList ')' ;

functionCall: funcName=IDENTIFIER '(' arguments=expressionList ')' ;

expressionList: expressions+=expression (',' expressions+=expression)*
              |
              ;

constructorCall: 'new' typeName=IDENTIFIER '(' arguments=expressionList ')'
			   | 'new' type=primitive '[' size=expression ']'
			   ;
              
object: primitive '[]'
	  | primitive
    | IDENTIFIER
	  ;

primitive: 'int'
		 | 'void'
		 | 'string'
		 ;
              
SYSTEMFUNC: 'toString'
		  | 'toInt'
		  | 'append'
		  ;


IDENTIFIER: [a-zA-Z][a-zA-Z0-9]* ;
NUMBER:  [0-9]+;
WHITESPACE: [ \t\n\r]+ -> skip;
STRING: '"' .*? '"';